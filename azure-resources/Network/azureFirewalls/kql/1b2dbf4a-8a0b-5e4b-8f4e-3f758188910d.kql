// Azure Resource Graph Query
// List all in-scope Azure Firewall resources, where the VNet is not associated to a DDoS Protection Plan
resources
| where type =~ "Microsoft.Network/azureFirewalls"
| where isempty(properties.virtualHub.id) or isnull(properties.virtualHub.id)
| mv-expand ipConfig = properties.ipConfigurations
| project
    name,
    firewallId = id,
    tags,
    vNetName = split(ipConfig.properties.subnet.id, "/", 8)[0],
    vNetId = tolower(substring(ipConfig.properties.subnet.id, 0, indexof(ipConfig.properties.subnet.id, "/subnet")))
| join kind=fullouter (
    resources
    | where type =~ "Microsoft.Network/ddosProtectionPlans"
    | mv-expand vNet = properties.virtualNetworks
    | project ddosProtectionPlanId = id, vNetId = tolower(vNet.id)
    )
    on vNetId
| where isempty(ddosProtectionPlanId)
| project recommendationId = "1b2dbf4a-8a0b-5e4b-8f4e-3f758188910d", name, id = firewallId, tags, param1 = strcat("vNet: ", vNetName), param2 = "ddosProtection: Disabled"
