// Azure Resource Graph Query
// Find all ExpressRoute Connections that are connected to ErGw3AZ or UltraPerformance gateway sku that don't have
// FastPath enabled for both the Gateway Bypass or Private Endpoint/Link service.
resources
| where type == "microsoft.network/connections"
| where properties.connectionType =~ 'expressroute'
| extend gatewayId = tostring(properties.virtualNetworkGateway1.id)
| join kind=inner (
    resources
    | where type =~ "Microsoft.Network/virtualNetworkGateways"
    | where properties.sku.name in~ ("ErGw3AZ", "UltraPerformance")
    | extend gatewayId = tostring(id)
) on gatewayId
| extend erGatewayBypass = tobool(properties.expressRouteGatewayBypass)
| extend privateLinkFastPath = tobool(properties.enablePrivateLinkFastPath)
| where not(erGatewayBypass) or not(privateLinkFastPath)
| project recommendationId = "f6a14b32-a727-4ace-b5fa-7b1c6bdff402", id, name, tags,
    param1 = iff(erGatewayBypass, "Enabled: Gateway Bypass", "Disabled: Gateway Bypass"),
    param2 = iff(privateLinkFastPath, "Enabled: PE FastPath", "Disabled: PE FastPath")
