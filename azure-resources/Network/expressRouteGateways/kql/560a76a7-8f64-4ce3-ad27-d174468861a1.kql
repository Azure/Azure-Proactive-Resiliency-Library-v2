// Azure Resource Graph Query
// Find what Express Route Gateways have allow Non Virtual Wan Traffic enabled

resources
| where type =~ "Microsoft.Network/expressRouteGateways"
| where properties.allowNonVirtualWanTraffic == "true"
| extend ergwId = tolower(tostring(id)), ergwName = name, ergwTags = tags, ergwLocation = location, vhubId = tolower(tostring(properties.virtualHub.id))
| join kind=leftouter (
    resources
    | where type =~ "Microsoft.Network/virtualHubs"
    | where ['kind'] != 'RouteServer'
    | extend vhubId = tolower(tostring(id)), vhubName = name, vwanId = tolower(tostring(properties.virtualWan.id))
) on $left.vhubId == $right.vhubId
| join kind=leftouter (
    resources
    | where type =~ "Microsoft.Network/virtualWans"
    | extend vwanId = tolower(tostring(id)), vwanName = name
) on $left.vwanId == $right.vwanId
| project recommendationId = "560a76a7-8f64-4ce3-ad27-d174468861a1", name, id, tags, param1=strcat("vHub Name: ", vhubName), param2=strcat("vWAN Name: ", vwanName)
