// Azure Resource Graph Query
// Display App Service for Apps under eligible App service plans and it shows if deployment slot is enabled or not

resources
| where type =~ 'microsoft.web/sites' or type =~ 'microsoft.web/sites/slots'
| extend Sku = tostring(properties.sku)
| where tolower(Sku) contains "standard" or tolower(Sku) contains "premium" or tolower(Sku) contains "isolatedv2"
| summarize count() by repositorySiteName = tostring(properties.repositorySiteName)
| where count_ == 1
| join kind=inner (
    resources
    | where type =~ 'microsoft.web/sites' //or type =~ 'microsoft.web/sites/slots'
    | extend repositorySiteName = tostring(properties.repositorySiteName)
    | extend Sku = tostring(properties.sku)
    | project id, name, subscriptionId, repositorySiteName, Sku
) on repositorySiteName
| project recommendationId="a1d91661-32d4-430b-b3b6-5adeb0975df7", name, id, tags="", param1=Sku, param2="DeploymentSlotEnabled=false"


